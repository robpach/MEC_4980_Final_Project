#include <Arduino.h>
#include "SparkFun_Qwiic_OLED.h"
#include <Buzzer.h>
#include <QuickPID.h>

// Thermistor variables
const float R = 10000.0;
const float Rtherm = 100000.0;
int sensorPin = A0;
float reading;
float T25 = 298.0;
float beta = 3750.0;
float tempK, voltage, resistance, tempC, tempF;
char disp[40];
unsigned long prevTime = 0;

// Temperature control variables
int target = 200; // Fahrenheit

// Buzzer Pin
const int buzzerPin = 2;

// PID Initialization
float Kp = 100, Ki = 1, Kd = 0.0; // Kp needs to be high otherwise heater is ineffective
float Input, Output, Setpoint;
QuickPID PID(&Input, &Output, &Setpoint);

// Initialize general pins
const int onButton = 3;
const int upButton = 6;
const int downButton = 5;
const int redLED = 10;
const int greenLED = 8;
const int tempSignal = 9;
bool onPressed = true;
bool upPressed = true;
bool downPressed = true;
bool currentOn;
unsigned long startTime;
unsigned long lastOLED = 0;
unsigned long timeRemaining = 0;
uint8_t progress = 0;
float prevTemp = 70.0;
unsigned long tempRateTime = 0;
float tempRate = 0;

// initialize objects
Buzzer buzzer(buzzerPin);
QwiicMicroOLED myOLED;

enum MachineStates
{
  Off,
  On
};

MachineStates currentState = Off;

void setup()
{
  delay(500);
  Serial.begin(9600);
  delay(500);

  pinMode(onButton, INPUT_PULLUP);
  pinMode(upButton, INPUT_PULLUP);
  pinMode(downButton, INPUT_PULLUP);
  pinMode(redLED, OUTPUT);
  pinMode(greenLED, OUTPUT);
  pinMode(tempSignal, OUTPUT);

  while (!myOLED.begin())
  {
    digitalWrite(redLED, HIGH);
    Serial.println("OLED failed");
    delay(500);
    digitalWrite(redLED, LOW);
    delay(500);
  }

  // Manual screen clearing to get rid of the "speckles"
  myOLED.text(0, 0, "clearingthescreen");
  myOLED.text(0, 10, "clearingthescreen");
  myOLED.text(0, 20, "clearingthescreen");
  myOLED.text(0, 30, "clearingthescreen");
  myOLED.text(0, 40, "clearingthescreen");
  myOLED.display();
  delay(500);
  myOLED.erase();
  myOLED.display();

  myOLED.text(0, 0, "Mug");
  myOLED.text(0, 10, "Warmer");
  myOLED.text(0, 30, "by Rob");
  myOLED.display();
  // add a startup sound here
  int delayTime = 120;
  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_E3, delayTime);

  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_D3, delayTime);

  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_C3, delayTime);
  
  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_AS2, delayTime);

  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_E2, delayTime);
  buzzer.sound(NOTE_B2, delayTime);
  buzzer.sound(NOTE_C3, delayTime);
  

  //NOTE_E2, 8, NOTE_E2, 8, NOTE_E3, 8, NOTE_E2, 8, NOTE_E2, 8, NOTE_D3, 8, NOTE_E2, 8, NOTE_E2, 8,
  //NOTE_C3, 8, NOTE_E2, 8, NOTE_E2, 8, NOTE_AS2, 8, NOTE_E2, 8, NOTE_E2, 8, NOTE_B2, 8, NOTE_C3, 8,

  Input = tempF;
  PID.SetTunings(Kp, Ki, Kd);
  PID.SetMode(1);
  PID.SetAntiWindupMode(1);
  PID.SetOutputLimits(0, 255);
}

void sensorFault()
{
  myOLED.erase();
  myOLED.text(0, 25, "FAULT!");
  myOLED.display();
  analogWrite(tempSignal, 0);
  digitalWrite(greenLED, LOW);
  while (true)
  {
    digitalWrite(redLED, HIGH);
    buzzer.sound(NOTE_G5, 500);
    digitalWrite(redLED, LOW);
    delay(100);
  }
}

void updateTemps()
{
  // calculates temperature in Kelvin, then changes to C and F
  reading = analogRead(sensorPin);
  voltage = float(reading / 1023) * 5;
  resistance = float(voltage * R) / float(5 - voltage);
  tempK = 1.0 / ((1.0 / T25) + 1.0 / beta * log(resistance / Rtherm));
  tempC = tempK - 273.15;
  tempF = tempC * (9.0 / 5.0) + 32.0;

  // Sensor Failure Check
  if (isnan(tempF) || tempF < -40 || tempF > 400)
  {
    sensorFault();
  }
}

void drawSineWave(int y0, int height, int width) // generated by chatGPT not gonna lie
{
    static float phase = 0;     // scroll offset
    const float freq = 0.60;  // wave frequency
    const float amp = height; // wave amplitude

    // Erase only the region we draw
    myOLED.rectangleFill(0, y0 - height - 1, width, (height * 2) + 2, 0);

    // Draw sine wave in this band
    for (int x = 0; x < width; x++)
    {
        float rad = (x + phase) * freq;
        int y = y0 + sin(rad) * amp;
        myOLED.pixel(x, y, 1);
    }

    // draw the bounds for the sine wave here:
    // something like 
    //myOLED.rectangleFill(0, y0 - height, width, 1, COLOR_WHITE);
    // and 
    //myOLED.rectangleFill(0, y0 + height, width, 1, COLOR_WHITE);

    //draw a rectangle around the whole sine wave, leaving empty space for where full heat (progress = 64) is
    myOLED.rectangle(0, y0 - height - 1, 64, (height * 2) + 2, COLOR_WHITE);
    //add a vertical line at the progress point
    myOLED.line(width, y0 - height - 1, width, y0 + height, COLOR_WHITE);



    phase += 2;   // makes the wave scroll
}

void displayTemps()
{
  // Prints both target temperature and actual temperature
  myOLED.erase();

  // Actual Temperature
  char tempStr[10];
  myOLED.text(0, 5, "Temp:");
  dtostrf(tempF, 3, 0, tempStr);
  snprintf(disp, sizeof(disp), "%s | %i F", tempStr, target);
  myOLED.text(0, 15, disp);
  //Serial.println(disp);

  // Target temperature
  /*myOLED.text(0, 25, "Target:");
  snprintf(disp, sizeof(disp), "%i F", target);
  myOLED.text(0, 35, disp);
  */

  // insert some sort of graphics at (x,y) = (0,30)


  // Time Remaining
  dtostrf(timeRemaining, 3, 0, tempStr);
  snprintf(disp, sizeof(disp), "%sm", tempStr);
  myOLED.text(40, 0, disp);
  // add a border around the time remaining

  // Create a progress bar
  progress = (tempF / (float)target) * 64.0;
  progress = (int)progress;
  progress = constrain(progress, 0, 64);
  //myOLED.rectangleFill(0, 25, progress, 2, COLOR_WHITE);

  // Sine wave animation
  drawSineWave(35, 5, progress);

  myOLED.display();
}

void loop()
{
  updateTemps();
  if (tempF > 120)
  {
    digitalWrite(redLED, HIGH);
  }
  else if (tempF < 120)
  {
    digitalWrite(redLED, LOW);
  }
  switch (currentState)
  {
  case Off:
    onPressed = currentOn;
    analogWrite(tempSignal, 0);

    // Reading the On button to switch to the On state
    currentOn = digitalRead(onButton);
    if (currentOn == LOW && onPressed == HIGH)
    {
      digitalWrite(greenLED, 100);
      onPressed = currentOn;
      buzzer.sound(NOTE_E5, 100);
      buzzer.sound(NOTE_F5, 100);
      buzzer.sound(NOTE_G5, 100);
      startTime = millis();
      currentState = On;
    }

    if (tempF > 120)
    {
      myOLED.erase();
      myOLED.text(10, 20, "Cooling...");
      myOLED.display();
      // display temps here underneath?
      // time until cool estimate?
    }
    else
    {
      myOLED.erase();
      myOLED.display();
    }

    break;

  case On:
    onPressed = currentOn;
    if (millis() - lastOLED > 50)
    {
      displayTemps();
      lastOLED = millis();
    }

    // Simple On/Off controll is used until the error is within 5 degrees F
    if ((target - tempF) > 5)
    {
      analogWrite(tempSignal, 255);
    }
    else if ((target - tempF) <= 5)
    {
      Setpoint = target;
      Input = tempF;
      PID.Compute();
      analogWrite(tempSignal, Output);
    }
    else
    {
      analogWrite(tempSignal, 0);
    }

    // Reading both up and down buttons to change target temperature
    bool currentUp = digitalRead(upButton);
    if (currentUp == LOW && upPressed == HIGH)
    {
      if (target >= 300)
      {
        target = 300;
        buzzer.sound(NOTE_G2, 100);
      }
      else
      {
        target = target + 5;
        buzzer.sound(NOTE_G5, 50);
      }
    }
    upPressed = currentUp;

    bool currentDown = digitalRead(downButton);
    if (currentDown == LOW && downPressed == HIGH)
    {
      if (target <= 100)
      {
        target = 100;
        buzzer.sound(NOTE_G2, 100);
      }
      else
      {
        target = target - 5;
        buzzer.sound(NOTE_C5, 50);
      }
    }
    downPressed = currentDown;

    // Reading the On button to switch back to the Off State
    currentOn = digitalRead(onButton);
    if (currentOn == LOW && onPressed == HIGH)
    {
      digitalWrite(greenLED, LOW);
      onPressed = currentOn;
      buzzer.sound(NOTE_G5, 100);
      buzzer.sound(NOTE_F5, 100);
      buzzer.sound(NOTE_E5, 100);
      myOLED.erase();
      myOLED.display();
      currentState = Off;
    }

    // 1 Hour shut down (when entering the "On" State)
    if ((millis() - startTime) >= 3600000UL)
    {
      digitalWrite(greenLED, LOW);
      onPressed = currentOn;
      buzzer.sound(NOTE_G5, 100);
      buzzer.sound(NOTE_F5, 100);
      buzzer.sound(NOTE_E5, 100);
      myOLED.erase();
      myOLED.display();
      currentState = Off;
    }

    timeRemaining = (3600000UL - (millis() - startTime)) / 60000UL;

    break;
  }

  delay(50);
}